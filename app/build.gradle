apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'

mainClassName = 'ua.com.elius.sm2csv.main'

run {
    workingDir = "$project.rootDir/work"
}

jar {
    manifest {
        attributes("Main-Class": mainClassName)
    }
}

def staticBinaryDestination = file("$rootDir/sbin")

shadowJar {
    baseName = rootProject.name
    classifier = ''
    destinationDir = staticBinaryDestination
}

launch4j {
    outfile = rootProject.name + '.exe'
    headerType = 'console'
    chdir = ''
    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "$rootDir/sbin/${project.tasks.shadowJar.archiveName}"
}

task sbin(type: Copy, dependsOn: 'launch4j', group: 'build') {
    from (project.tasks.launch4j.outputs) {
        include '*.exe'
    }
    into staticBinaryDestination
}

clean {
    delete staticBinaryDestination
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.apache.commons:commons-csv:1.2'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'commons-io:commons-io:2.4'
}
